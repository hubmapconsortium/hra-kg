PREFIX ccf: <http://purl.org/ccf/>
PREFIX CCF: <https://purl.humanatlas.io/graph/ccf>
PREFIX DSGraphs: <https://purl.humanatlas.io/collection/ds-graphs>
PREFIX has_characterizing_biomarker_set: <http://purl.obolibrary.org/obo/RO_0015004>

CONSTRUCT {
  ?rui_location ccf:collides_with ?anatomical_structure ;
    ccf:collides_with_ct ?cell_type ;
    ccf:collides_with_bm ?biomarker .
}
FROM CCF:
FROM DSGraphs:
WHERE {
  {
    ?rui_location ccf:collides_with ?anatomical_structure .
  }
  UNION
  {
    [] rdf:type ccf:SpatialPlacement ;
        ccf:placement_relative_to [
          ccf:representation_of ?anatomical_structure
        ] ;
        ccf:placement_for ?rui_location ;
  }
  UNION
  {
    [] rdf:type ccf:SpatialPlacement ;
        ccf:placement_relative_to [
          ccf:representation_of ?as_child
        ] ;
        ccf:placement_for ?rui_location .
    ?as_child ccf:ccf_part_of ?anatomical_structure .
  }

  {
    ?cell_type ccf:ccf_located_in ?anatomical_structure . 
  }
  UNION
  {
    ?anatomical_structure ^ccf:ccf_located_in [
      ccf:ccf_ct_isa* ?cell_type
    ] .
  }

  {
    ?cell_type rdfs:subClassOf [
      owl:onProperty has_characterizing_biomarker_set: ;
      owl:someValuesFrom [ owl:intersectionOf ?bn3 ]] .
    ?bn3 rdf:rest*/rdf:first [
      owl:onProperty ccf:has_marker_component ;
      owl:someValuesFrom ?biomarker
    ] .
  }
}
