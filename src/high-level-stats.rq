SELECT ?label ?count
WHERE {

  {
    SELECT ("# Nodes" as ?label) (COUNT(*) as ?count)
    WHERE {
      SELECT DISTINCT ?s 
      WHERE {
        {
          ?s ?p1 [] .
        }
        UNION
        {
          [] ?p2 ?s .
          FILTER(isIRI(?s)) 
        }
      } 
    }
  }
  UNION
  {
    SELECT ("# Edges" as ?label) (COUNT(*) as ?count)
    WHERE {
      ?s ?p ?o .
    }
  }
  UNION
  {
    SELECT ("# Edge Types" as ?label) (COUNT(*) as ?count)
    WHERE {
      SELECT DISTINCT ?p
      WHERE {
        ?s ?p ?o .
      }
    }
  }
}
